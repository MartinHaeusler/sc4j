buildscript {
	repositories {
	  jcenter()
	}
}

apply plugin: 'java-library'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    mavenCentral();
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


// configure the group and version for this project
group = 'com.github.martinhaeusler'
version = '0.1-SNAPSHOT'

jar {
	manifest {
		attributes 'Implementation-Version': version
		attributes 'provider': 'com.github.martinhaeusler'
	}
}

javadoc{
	failOnError = false
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
	
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
	
artifacts {
   	archives javadocJar, sourcesJar
}
	
signing {
	sign configurations.archives
}
	
uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
	
      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }
	
      pom.project {
        name 'sc4j'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Schema Configuration for Java.'
        url 'https://github.com/MartinHaeusler/sc4j'
	
        scm {
          connection 'scm:git:https://github.com/MartinHaeusler/sc4j'
          developerConnection 'scm:git:https://github.com/MartinHaeusler/sc4j'
          url 'https://github.com/MartinHaeusler/sc4j'
        }
	
        licenses {
          license {
            name 'GNU Affero General Public License v3'
            url 'https://www.gnu.org/licenses/agpl-3.0.txt'
          }
        }
	
        developers {
          developer {
            id 'martin.haeusler'
            name 'Martin HÃ¤usler'
            email 'martin.haeusler89@gmail.com'
          }
        }
      }
    }
  }
}

dependencies {
   compile group: 'com.google.guava', name: 'guava', version: '21.0'
   compile group: 'commons-configuration', name: 'commons-configuration', version: '1.10'
   compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.24'

   testCompile group: 'junit', name: 'junit', version: '4.12'
   testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.24'
}

